buildscript {
    ext.kotlin_version = '1.2.61'
    ext.lifecycle_version = "1.1.1"
}

plugins {
    id "org.jetbrains.kotlin.jvm" version '1.2.61'
    id "maven"
    id "signing"
}

def libName = 'executordecorator-android-arch-extensions'
group libGroup
archivesBaseName = libName
version libVersion

repositories {
    google()
    mavenCentral()
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation project(':executordecorator-annotations')
    implementation "android.arch.lifecycle:common-java8:$lifecycle_version"

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:3.11.2'

}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    archiveClassifier.set('javadoc')
    from javadoc
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://s01.oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name libName
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Android extensions library which helps mutate decorator considering android lifecycle'
                url 'https://github.com/sagix/executordecorator'

                scm {
                    connection 'scm:git:https://github.com/sagix/executordecorator.git'
                    developerConnection 'scm:git:ssh:github.com:sagix/executordecorator.git'
                    url 'https://github.com/sagix/executordecorator'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'sagix'
                        name 'Nicolas Mouchel'
                        email 'sagix.ink@gmail.com'
                    }
                }
            }
        }
    }
}